no-vdom
--------

使用 haxe 的宏, 在编译时自动将对 "组件" 的操作, 转换成对 DOM 元素的直接操作。

## 特性

* 非侵入式，即: 你不需要写额外的模板, no-vdom 在编译时能自已解析 html 文件中的指定的 HTML 片段。

* 高效地定义组件，并且在运行时不会损失任何性能，因为组件在编译后不会产生任何额外的代码。

* 编译阶段检测"组件", 并通过 IDE 定位错误的所在文件和错误.

* 不依赖任何 js 库, 连 jquery 也都不需要

* 浏览器支持 IE8+

  > 编译时需要添加 `-D js-es=3`, 或者自已添加 polyfill, 例如: `Array.prototype.indexOf`

## 依赖

* 只能在 haxe 中使用, 推荐使用 haxe git 版本, 可在 <build.haxe.org> 下载或者自已编译.

* 需要 [haxelib csss](https://github.com/R32/css-selector)

  > 这个 haxe 库用在编译阶段, 并不会产生任何一个字节的 js 代码

## 示例

示例使用 vscode 作为 IDE. 下边是 html 文件的主要部分.

```html
<div>
  <!--  hello world -->
  <div class="sec t01">
    <h4>hello world</h4>
  </div>

  <!--  Tick  -->
  <div class="sec t02">
    <h4>tick <span>0</span></h4>
  </div>

  <!--  TODO  -->
  <div class="sec t03">
    <h4>TODO</h4>
    <form>
        <input type="text" />
        <input type="button" value="Add" />
    </form>
    <ul class="todo-list"></ul>
  </div>
</div>
<script type="text/javascript" src="demo.js"></script>
```

### hello world

```hx
// 定义组件
@:build(Nvd.build("index.html", ".t01 h4", {
	text: Prop("", "textContent")
})) abstract HelloWorld(nvd.Comp) {
}
```

```hx
// 在 Demo.hx 中调用:
class Demo {
  static function main() {
    var t01 = HelloWorld.ofSelector(".t01 h4");
    t01.text = "你好, 世界!";
  }
}
```

编译: `haxe -dce full -D analyzer-optimize -main Demo -lib no-vdom -js demo.js`

```js
// Generated by Haxe 4.0.0 (git build development @ e6f3b7d)
(function () { "use strict";
var Demo = function() { };
Demo.main = function() {
    window.document.querySelector(".t01 h4").textContent = "你好, 世界!";
};
Demo.main();
})();
```

回到组件的语法定义上, `@:build(file, css, def) ....`:

* 第一个参数表示相对于当前项目的文件路径, 可以是任何 html 或者 xml 文件。

* 第二个参数为一个 css 选择字符串, 这个用于确定 "组件" 的顶层元素

* 第三个参数定义了这个 "组件" 有哪些属性, 例如上边示例中的 text。 目前支持三种挂接类型：

  - `Elem(sel)`: 表示选择一个元素, sel 表示其子元素的 css 选择器。

    > 注: 当 sel 为 `null` 或空字符串(`""`) 时, 表示选择自已, 即顶层元素。

  - `Prop(sel, name)`: 表示远择指定元素(sel) 的 property.

  - `Attr(sel, name)`: 表示远择指定元素(sel) 的 attribute.

  顺便说一下, no-vdom 能自动识别这些属性的类型, 例如: `Prop(sel, "className")` 是 String, 而 `Prop(s, "offsetLeft")` 则能自动识别为 Int 类型.

  如果 no-vdom 识别不了, 那么在编译时将会报错, 这时很可能是你不小心写错了，或者 mozilla 所定义的标准中不存在那个属性。

  **issue**: 目前暂时无法识别 svg 中的子元素类型（添加这些实在太累了...）

* 使用 `abstract` （抽象类）来定义"组件"


注: 上边示例在 IE8 中无法正常运行, 因为 IE8 不支持 textContent 属性, 因此 no-vdom 提供了一个 "定制" 的属性, "text",
因此, 将上边组件改为下边, 并且编译时添加 "-D js-es=3"

```hx
@:build(Nvd.build("index.html", ".t01 h4", {
    text: Prop("", "text")
})) abstract HelloWorld(nvd.Comp) {
}
```

```js
// 编译后为:
dt.setText(window.document.querySelector(".t01 h4"),"你好, 世界!");
```

> 看上去为一个 hello world 就已经说了这么多内容, 但这些已经是 no-vdom 的所有内容了。

### demo-Tick

```hx
// 组件定义:
@:build(Nvd.build("index.html", ".t02 h4", {
  ts: Prop("span", "text")
})) abstract Tick(nvd.Comp) {
  // 定义一个组件方法, 最好是 inline 类型, 这样不会生成任何与“组件”有关的代码
  public inline function run(timer) {
    timer.run = function() {
      ts = "" + (Std.parseInt(ts) + 1);
    }
  }
}
```

```hx
// 在 hx 代码中调用:
var t02 = Tick.ofSelector(".t02 h4");
t02.run(new haxe.Timer(1000));
```

```js
// 编译后的相关代码:
var this1 = window.document.querySelector(".t02 h4");
new haxe_Timer(1000).run = function() {
  dt.setText(this1.children[0],"" + (Std.parseInt(dt.getText(this1.children[0])) + 1));
};
```

### demo-form

```hx
// 组件: (这个示例, 看上去不太优美。)
@:build(Nvd.build("index.html", "#login", {
  btn: Elem("button[type=submit]"),
  name: Prop("input[name=name]", "value"),
  email: Prop("input[name=email]", "value"),
  remember: Prop("input[type=checkbox]", "checked"),
#if (js_es >= 5)
  // IE8 did not support the pseudo-selector ":checked"
  // the last argument "true" is used to keep the css-selector to find in runtime
  herpderp: Prop("input[type=radio][name=herpderp]:checked", "value", true),
#end
})) abstract LoginForm(nvd.Comp) {
  public inline function getData() {
  #if (js_es < 5)
    var herpderp = null;
    var a: Array<js.html.InputElement> = cast this.querySelectorAll("input[name=herpderp]");
    for (r in a)
      if (r.checked) herpderp = r.value;
  #end
    return {
      name: name,
      email: email,
      remember: remember,
      herpderp: herpderp,
    }
  }
}
```

![screen shot](demo/demo.gif)

![screen shot](demo/demo-2.gif)

```hx
// 在 haxe 中调用:
var login = LoginForm.ofSelector("#login");
login.btn.onclick = function() {
  trace(login.getData());
}
```

```js
// 编译后的相关代码::
var login = window.document.querySelector("#login");
login.children[2].children[3].onclick = function() {
  console.log("Demo.hx:9:",{ name : login.children[0].children[1].value, email : login.children[1].children[1].value, remember : login.children[2].children[0].children[0].checked, herpderp : login.querySelector("input[type=radio][name=herpderp]:checked").value});
};
```

所这些示例文件在 [demo](demo/Demo.hx?ts=4) 中
